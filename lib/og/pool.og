!og

import
  time
  "github.com/champii/og/lib/common"

type WorkerCallback (fn(*common.File): error)

struct Worker
  In chan *common.File
  Out chan error
  Processing *common.File
  Job WorkerCallback

  *Run ->
    for todo in @In
      @Processing = todo

      res := @Job(todo)

      @Processing = nil

      @Out <- res

NewWorker(i chan *common.File, out chan error, job WorkerCallback): *Worker ->
  &Worker
    Processing: nil
    In: i
    Out: out
    Job: job

struct Pool
  Size int
  Workers []*Worker
  Total int
  Finished int
  In chan *common.File
  Out chan error

  Queue(job *common.File) ->
    @In <- job

  Run: error ->
    for _, worker in @Workers
      go worker.Run()

    close(@In)

    common.Print.CursorHide()
    defer common.Print.CursorShow()

    ticker := time.NewTicker(100 * time.Millisecond)
    go ->
      for true
        <-ticker.C
        @Print()

    for @Finished < @Total
      if err := <-@Out; err != nil
        ticker.Stop()
        return err

      @Finished++

    ticker.Stop()
    @Print()

    nil

  Print ->
    workerIds := []int{}
    files := []string{}

    for i, worker in @Workers
      if worker.Processing != nil
        files = append(files, worker.Processing.Path)
        workerIds = append(workerIds, i + 1)

    common.Print.CompileList(files, workerIds, len(@Workers), @Finished, @Total)

NewPool(size int, nbJobs int, cb WorkerCallback): *Pool ->
  pool := &Pool
    Size: size
    Total: nbJobs
    Finished: 0
    In: make(chan *common.File, nbJobs)
    Out: make(chan error, nbJobs)

  for i := 0; i < pool.Size; i++
    pool.Workers = append(pool.Workers, NewWorker(pool.In, pool.Out, cb))

  pool