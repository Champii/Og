!ast

import
	strings

struct SourceFile
	*Node
	Package  *Package
	Import   *Import
	TopLevels []*TopLevel
	Eval: string ->
		res := ""
		if @Package != nil => res += @Package.Eval() + "\n"
		if @Import  != nil => res += @Import.Eval() + "\n"
		for _, t in @TopLevels
			res += t.Eval() + "\n"
		res

struct Package
	*Node
	Name string
	Eval: string -> "package " + @Name

struct Import
	*Node
	Items []*ImportSpec
	Eval: string ->
		res := "import (\n"
		for _, i in @Items
			res += i.Eval()
		res + "\n)\n"

struct ImportSpec
	*Node
	Path  string
	Alias string
	Eval: string ->
		res := ""
		if len(@Alias) > 0 => res += @Alias + " "
		res += @Path
		res

struct TopLevel
	*Node
	Declaration  *Declaration
	FunctionDecl *FunctionDecl
	MethodDecl   *MethodDecl
	Eval: string ->
		if @Declaration  != nil => return @Declaration.Eval()
		if @FunctionDecl != nil => return @FunctionDecl.Eval()
		if @MethodDecl   != nil => return @MethodDecl.Eval()
		""

struct Declaration
	*Node
	ConstDecl *ConstDecl
	TypeDecl  *TypeDecl
	VarDecl   *VarDecl
	Eval: string ->
		if @ConstDecl != nil => return @ConstDecl.Eval()
		if @TypeDecl  != nil => return @TypeDecl.Eval()
		if @VarDecl   != nil => return @VarDecl.Eval()
		""

struct ConstDecl
	*Node
	ConstSpecs []*ConstSpec
	Eval: string ->
		res := "const ("
		for _, spec in @ConstSpecs
			res += spec.Eval() + "\n"
		res + ")"

struct ConstSpec
	*Node
	IdentifierList []string
	Type           *Type
	ExpressionList *ExpressionList
	Eval: string ->
		res := strings.Join(@IdentifierList, ",")
		if @Type           != nil => res += " " + @Type.Eval()
		if @ExpressionList != nil => res += " = " + @ExpressionList.Eval()
		res

struct ExpressionList
	*Node
	Expressions []*Expression
	Eval: string ->
		res := ""
		for _, expr in @Expressions
			res += expr.Eval() + ","
		if len(@Expressions) > 0
			res = res[:len(res)-1]
		res

struct Parameters
	*Node
	List []*Parameter
	Eval: string ->
		res := "("
		for _, expr in @List
			res += expr.Eval() + ","
		if len(@List) > 0
			res = res[:len(res)-1]
		res += ")"
		res

struct TypeDecl
	*Node
	TypeSpecs     []*TypeSpec
	StructType    *StructType
	InterfaceType *InterfaceType
	Eval: string ->
		res := "type "
		if len(@TypeSpecs) > 1
			res += "("
		for _, spec in @TypeSpecs
			res += spec.Eval() + "\n"
		if len(@TypeSpecs) > 0
			res = res[:len(res)-1]
		if len(@TypeSpecs) > 1
			res += ")"
		if @StructType         != nil => res += @StructType.Eval()
		else if @InterfaceType != nil => res += @InterfaceType.Eval()
		res

struct TypeSpec
	*Node
	Name string
	Type *Type
	Eval: string -> @Name + " " + @Type.Eval()

struct FunctionDecl
	*Node
	Name      string
	Function  *Function
	Signature *Signature
	Eval: string ->
		res := "func " + @Name
		if @Function  != nil => res += @Function.Eval()
		if @Signature != nil => res += @Signature.Eval()
		res

struct Function
	*Node
	Signature        *Signature
	Block            *Block
	Eval: string ->  @Signature.Eval() + @Block.Eval()

struct MethodDecl
	*Node
	Receiver *Receiver
	Function  *Function
	Signature *Signature
	Eval: string ->
		res := "func " + @Receiver.Eval()
		if @Function  != nil => res += @Function.Eval()
		if @Signature != nil => res += @Signature.Eval()
		res

struct Receiver
	*Node
	Package           string
	IsPointerReceiver bool
	Method            string
	Eval: string ->
		res := "(this "
		if @IsPointerReceiver => res += "*"
		res += @Package + ")"
		res += @Method
		res

struct VarDecl
	*Node
	VarSpecs []*VarSpec
	Eval: string ->
		res := "var ("
		for _, spec in @VarSpecs
			res += spec.Eval() + "\n"
		res + ")"

struct VarSpec
	*Node
	IdentifierList []string
	Type           *Type
	ExpressionList *ExpressionList
	Eval: string ->
		res := strings.Join(@IdentifierList, ",")
		if @Type           != nil => res += " " + @Type.Eval()
		if @ExpressionList != nil => res += "=" + @ExpressionList.Eval()
		res

struct Block
	*Node
	Statements []*Statement
	Eval: string ->
		res := "{\n"
		for _, spec in @Statements
			res += spec.Eval() + "\n"
		res + "}"

struct Statement
	*Node
	SimpleStmt      *SimpleStmt
	LabeledStmt     *LabeledStmt
	GoStmt          *GoStmt
	ReturnStmt      *ReturnStmt
	BreakStmt       *BreakStmt
	ContinueStmt    *ContinueStmt
	GotoStmt        *GotoStmt
	FallthroughStmt *FallthroughStmt
	IfStmt          *IfStmt
	SwitchStmt      *SwitchStmt
	SelectStmt      *SelectStmt
	ForStmt         *ForStmt
	Block           *Block
	DeferStmt       *DeferStmt
	Eval: string ->
		if @SimpleStmt      != nil => return @SimpleStmt.Eval()
		if @LabeledStmt     != nil => return @LabeledStmt.Eval()
		if @GoStmt          != nil => return @GoStmt.Eval()
		if @ReturnStmt      != nil => return @ReturnStmt.Eval()
		if @BreakStmt       != nil => return @BreakStmt.Eval()
		if @ContinueStmt    != nil => return @ContinueStmt.Eval()
		if @GotoStmt        != nil => return @GotoStmt.Eval()
		if @FallthroughStmt != nil => return @FallthroughStmt.Eval()
		if @IfStmt          != nil => return @IfStmt.Eval()
		if @SwitchStmt      != nil => return @SwitchStmt.Eval()
		if @SelectStmt      != nil => return @SelectStmt.Eval()
		if @ForStmt         != nil => return @ForStmt.Eval()
		if @Block           != nil => return @Block.Eval()
		if @DeferStmt       != nil => return @DeferStmt.Eval()
		""

struct SimpleStmt
	*Node
	SendStmt       *SendStmt
	Expression     *Expression
	IncDecStmt     *IncDecStmt
	ShortVarDecl   *ShortVarDecl
	Assignment     *Assignment
	EmptyStmt      bool
	Eval: string ->
		if @SendStmt     != nil => return @SendStmt.Eval()
		if @Expression   != nil => return @Expression.Eval()
		if @IncDecStmt   != nil => return @IncDecStmt.Eval()
		if @ShortVarDecl != nil => return @ShortVarDecl.Eval()
		if @Assignment   != nil => return @Assignment.Eval()
		if @EmptyStmt           => return "\n"
		""

struct SendStmt
	*Node
	Left  *Expression
	Right *Expression
	Eval: string -> @Left.Eval() + "<-" + @Right.Eval()

struct IncDecStmt
	*Node
	Expression *Expression
	IsInc      bool
	Eval: string ->
		res := @Expression.Eval()
		if @IsInc => res += "++"
		else      => res += "--"
		res

struct Assignment
	*Node
	Left  *ExpressionList
	Op    string
	Right *ExpressionList
	Eval: string -> @Left.Eval() + @Op + @Right.Eval()

struct ShortVarDecl
	*Node
	Identifiers []string
	Expressions *ExpressionList
	// Statement   *Statement
	Eval: string ->
		res := ""
		res += strings.Join(@Identifiers, ",") + ":="
		if @Expressions != nil => res += @Expressions.Eval()
		// if @Statement   != nil => res += @Statement.Eval()
		res

struct LabeledStmt
	*Node
	Name      string
	Statement *Statement
	Eval: string -> @Name + ": " + @Statement.Eval()

struct ReturnStmt
	*Node
	Expressions *ExpressionList
	Eval: string ->
		res := "return "
		if @Expressions != nil => res += @Expressions.Eval()
		res

struct BreakStmt
	*Node
	Name string
	Eval: string -> "break " + @Name

struct ContinueStmt
	*Node
	Name string
	Eval: string -> "continue " + @Name

struct GotoStmt
	*Node
	Name string
	Eval: string -> "goto " + @Name

struct FallthroughStmt
	*Node
	Eval: string -> "fallthrough"

struct DeferStmt
	*Node
	Expression *Expression
	Eval: string -> "defer " + @Expression.Eval()

struct IfStmt
	*Node
	SimpleStmt *SimpleStmt
	Expression *Expression
	Block      *Block
	IfStmt     *IfStmt
	BlockElse  *Block
	Eval: string ->
		res := "if "
		if @SimpleStmt != nil => res += @SimpleStmt.Eval() + ";"
		res += @Expression.Eval()
		if @Block      != nil => res += @Block.Eval()
		if @BlockElse  != nil => res += "else " + @BlockElse.Eval()
		if @IfStmt     != nil => res += "else " + @IfStmt.Eval()
		res

struct SwitchStmt
	*Node
	ExprSwitchStmt *ExprSwitchStmt
	TypeSwitchStmt *TypeSwitchStmt
	Eval: string ->
		if @ExprSwitchStmt != nil => return @ExprSwitchStmt.Eval()
		if @TypeSwitchStmt != nil => return @TypeSwitchStmt.Eval()
		""

struct ExprSwitchStmt
	*Node
	SimpleStmt      *SimpleStmt
	Expression      *Expression
	ExprCaseClauses []*ExprCaseClause
	Eval: string ->
		res := "switch "
		if @SimpleStmt != nil => res += @SimpleStmt.Eval() + ";"
		if @Expression != nil => res += @Expression.Eval()
		res += "{\n"
		for _, spec in @ExprCaseClauses
			res += spec.Eval() + "\n"
		res + "}"

struct ExprCaseClause
	*Node
	ExprSwitchCase *ExprSwitchCase
	Statements     []*Statement
	Eval: string ->
		res := ""
		res += @ExprSwitchCase.Eval() + ":"
		for _, spec in @Statements
			res += spec.Eval() + "\n"
		if len(@Statements) > 0
			res = res[:len(res)-1]
		res

struct ExprSwitchCase
	*Node
	Expressions *ExpressionList
	IsDefault   bool
	Eval: string ->
		if @IsDefault => return "default"
		"case " + @Expressions.Eval()

struct TypeSwitchStmt
	*Node
	SimpleStmt       *SimpleStmt
	TypeSwitchGuard  *TypeSwitchGuard
	TypeCaseClauses  []*TypeCaseClause
	Eval: string ->
		res := "switch "
		if @SimpleStmt != nil => res += @SimpleStmt.Eval() + ";"
		res += @TypeSwitchGuard.Eval()
		res += "{\n"
		for _, spec in @TypeCaseClauses
			res += spec.Eval() + "\n"
		res + "}"

struct TypeSwitchGuard
	*Node
	Name        string
	PrimaryExpr *PrimaryExpr
	Eval: string ->
		res := ""
		if len(@Name) > 0 => res += @Name + "="
		res + @PrimaryExpr.Eval() + ".(type)"

struct TypeCaseClause
	*Node
	TypeSwitchCase *TypeSwitchCase
	Statements     []*Statement
	Eval: string ->
		res := @TypeSwitchCase.Eval() + ":"
		for _, spec in @Statements
			res += spec.Eval() + "\n"
		if len(@Statements) > 0
			res = res[:len(res)-1]
		res

struct TypeSwitchCase
	*Node
	Types []*Type
	Eval: string ->
		res := ""
		if len(@Types) == 0 => return "default"
		res += "case "
		for _, spec in @Types
			res += spec.Eval() + ","
		res = res[:len(res)-1]
		res

struct SelectStmt
	*Node
	CommClauses []*CommClause
	Eval: string ->
		res := "select {\n"
		for _, spec in @CommClauses
			res += spec.Eval()
		res + "}"

struct CommClause
	*Node
	CommCase *CommCase
	Block *Block
	Eval: string ->
		block := @Block.Eval()
		@CommCase.Eval() + ":" + block[1:len(block)-1]

struct CommCase
	*Node
	SendStmt *SendStmt
	RecvStmt *RecvStmt
	IsDefault bool
	Eval: string ->
		if @IsDefault       => return "default"
		if @SendStmt != nil => return "case " + @SendStmt.Eval()
		if @RecvStmt != nil => return "case " + @RecvStmt.Eval()
		""

struct RecvStmt
	*Node
	Expressions *ExpressionList
	Identifiers []string
	Expression  *Expression
	Eval: string ->
		res := ""
		if @Expressions != nil => res += @Expressions.Eval() + "="
		res += strings.Join(@Identifiers, ",")
		if len(@Identifiers) > 0 => res += ":="
		res += @Expression.Eval()
		res

struct ForStmt
	*Node
	Expression  *Expression
	ForClause   *ForClause
	RangeClause *RangeClause
	Block       *Block
	Eval: string ->
		res := "for "
		if @Expression  != nil => res += @Expression.Eval()
		if @ForClause   != nil => res += @ForClause.Eval()
		if @RangeClause != nil => res += @RangeClause.Eval()
		res + @Block.Eval()

struct ForClause
	*Node
	LeftSimpleStmt  *SimpleStmt
	Expression      *Expression
	RightSimpleStmt *SimpleStmt
	Eval: string ->
		res := ""
		if @LeftSimpleStmt  != nil => res += @LeftSimpleStmt.Eval()
		res += ";"
		if @Expression      != nil => res += @Expression.Eval()
		res += ";"
		if @RightSimpleStmt != nil => res += @RightSimpleStmt.Eval()
		res

struct RangeClause
	*Node
	// Expressions *ExpressionList
	Identifiers []string
	Expression  *Expression
	Eval: string ->
		res := ""
		// if @Expressions != nil => res += @Expressions.Eval()
		res += strings.Join(@Identifiers, ",")
		res += ":= "
		res + "range " +  @Expression.Eval()

struct GoStmt
	*Node
	Function   *Function
	Expression *Expression
	Eval: string ->
		if @Function   != nil => return "go func " + @Function.Eval() + "()"
		if @Expression != nil => return "go " + @Expression.Eval()
		""

struct Type
	*Node
	TypeName string
	TypeLit  *TypeLit
	Type     *Type
	Eval: string ->
		if @TypeLit != nil => return @TypeLit.Eval()
		if @Type    != nil => return "(" + @Type.Eval() + ")"
		@TypeName

struct TypeLit
	*Node
	ArrayType     *ArrayType
	StructType    *StructType
	PointerType   *PointerType
	FunctionType  *FunctionType
	InterfaceType *InterfaceType
	SliceType     *SliceType
	MapType       *MapType
	ChannelType   *ChannelType
	Eval: string ->
		if @ArrayType     != nil => return @ArrayType.Eval()
		if @StructType    != nil => return @StructType.Eval()
		if @PointerType   != nil => return @PointerType.Eval()
		if @FunctionType  != nil => return @FunctionType.Eval()
		if @InterfaceType != nil => return @InterfaceType.Eval()
		if @SliceType     != nil => return @SliceType.Eval()
		if @MapType       != nil => return @MapType.Eval()
		if @ChannelType   != nil => return @ChannelType.Eval()
		""

struct ArrayType
	*Node
	Length      *Expression
	ElementType *Type
	Eval: string -> "[" + @Length.Eval() + "]" + @ElementType.Eval()

struct PointerType
	*Node
	Type *Type
	Eval: string -> "*" + @Type.Eval()

struct InterfaceType
	*Node
	Name string
	MethodSpecs []*MethodSpec
	Eval: string ->
		res := ""
		if len(@MethodSpecs) > 0
			res += "\n"
		for _, spec in @MethodSpecs
			res += spec.Eval() + "\n"
		@Name + " interface {" + res  + "}"

struct SliceType
	*Node
	Type *Type
	Eval: string -> "[]" + @Type.Eval()

struct MapType
	*Node
	InnerType *Type
	OuterType *Type
	Eval: string -> "map[" + @InnerType.Eval() + "]" + @OuterType.Eval()

struct ChannelType
	*Node
	ChannelDecl string
	Type        *Type
	Eval: string -> @ChannelDecl + " " + @Type.Eval()

struct MethodSpec
	*Node
	Name string
	Parameters *Parameters
	Result     *Result
	Type       string
	Eval: string ->
		res := @Name
		if @Parameters != nil => res += @Parameters.Eval()
		if @Result     != nil => res += @Result.Eval()
		res += @Type
		res

struct FunctionType
	*Node
	Signature *Signature
	Eval: string -> "func " + @Signature.Eval()

struct Signature
	*Node
	Parameters *Parameters
	Result     *Result
	Eval: string ->
		res := ""
		if @Parameters != nil => res += @Parameters.Eval()
		if @Result != nil => res += @Result.Eval()
		res

struct Result
	*Node
	Types []*Type
	Eval: string ->
		res := "("
		for _, spec in @Types
			res += spec.Eval() + ","
		res += ")"
		res

struct Parameter
	*Node
	Names      []string
	Type       *Type
	IsVariadic bool
	Eval: string ->
		res := ""
		for _, spec in @Names
			res += spec + ","
		if len(@Names) > 0
			res = res[:len(res)-1]
		res += " "
		if @IsVariadic => res += "..."
		res + @Type.Eval()

struct Operand
	*Node
	Literal     *Literal
	OperandName *OperandName
	MethodExpr  *MethodExpr
	Expression  *Expression
	Eval: string ->
		if @Literal     != nil => return @Literal.Eval()
		if @OperandName != nil => return @OperandName.Eval()
		if @MethodExpr  != nil => return @MethodExpr.Eval()
		if @Expression  != nil => return "(" + @Expression.Eval() + ")"
		""

struct Literal
	*Node
	Basic     string
	Composite *CompositeLit
	FunctionLit     *FunctionLit
	Eval: string ->
		if @Composite != nil => return @Composite.Eval()
		if @FunctionLit != nil => return @FunctionLit.Eval()
		@Basic

struct OperandName
	*Node
	Name string
	Eval: string -> @Name

struct CompositeLit
	*Node
	LiteralType  *LiteralType
	LiteralValue *LiteralValue
	Eval: string -> @LiteralType.Eval() + @LiteralValue.Eval()

struct LiteralType
	*Node
	Struct   *StructType
	Array    *ArrayType
	Element  *Type
	Slice    *SliceType
	Map      *MapType
	Type     string
	Eval: string ->
		if @Struct  != nil => return @Struct.Eval()
		if @Array   != nil => return @Array.Eval()
		if @Element != nil => return "[...]" + @Element.Eval()
		if @Slice   != nil => return @Slice.Eval()
		if @Map     != nil => return @Map.Eval()
		@Type

struct LiteralValue
	*Node
	Elements []*KeyedElement
	Eval: string ->
		res := "{"
		if len(@Elements) > 1
			res += "\n"
		for _, spec in @Elements
			res += spec.Eval() + ",\n"
		res + "}"

struct KeyedElement
	*Node
	Key     *Key
	Element *Element
	Eval: string ->
		res := ""
		if @Key != nil => res += @Key.Eval() + ":"
		res + @Element.Eval()

struct Key
	*Node
	Name         string
	Expression   *Expression
	LiteralValue *LiteralValue
	Eval: string ->
		if @Expression   != nil => return @Expression.Eval()
		if @LiteralValue != nil => return @LiteralValue.Eval()
		@Name

struct Element
	*Node
	Expression   *Expression
	LiteralValue *LiteralValue
	Eval: string ->
		if @Expression   != nil => return @Expression.Eval()
		if @LiteralValue != nil => return @LiteralValue.Eval()
		""

struct StructType
	*Node
	Name string
	Fields []*FieldDecl
	Eval: string ->
		res := @Name + " struct {\n"
		methods := ""
		for _, spec in @Fields
			if spec.InlineStructMethod != nil
				receiver := @Name
				if spec.InlineStructMethod.IsPointerReceiver => receiver = "*" + receiver
				methods += "\nfunc (this " + receiver + ")" + spec.InlineStructMethod.Eval()
			else
				res += spec.Eval() + "\n"
		res + "}" + methods

struct FieldDecl
	*Node
	Identifiers []string
	Type        *Type
	Anonymous   *AnonymousField
	Tag         string
	InlineStructMethod *InlineStructMethod
	Eval: string ->
		if @InlineStructMethod != nil => return @InlineStructMethod.Eval()
		res := strings.Join(@Identifiers, ",")
		if @Type != nil => res += " " + @Type.Eval()
		if @Anonymous != nil => res += @Anonymous.Eval()
		res + " " + @Tag

struct InlineStructMethod
	*Node
	IsPointerReceiver bool
	FunctionDecl      *FunctionDecl
	Eval: string -> @FunctionDecl.Eval()[4:]

struct AnonymousField
	*Node
	IsPointerReceiver bool
	Type              string
	Eval: string ->
		res := @Type
		if @IsPointerReceiver => res = "*" + res
		res

struct FunctionLit
	*Node
	Function *Function
	Eval: string -> "func " + @Function.Eval()

struct PrimaryExpr
	*Node
	Operand       *Operand
	Conversion    *Conversion
	PrimaryExpr   *PrimaryExpr
	SecondaryExpr *SecondaryExpr
	Eval: string ->
		if @Operand       != nil => return @Operand.Eval()
		if @Conversion    != nil => return @Conversion.Eval()
		if @PrimaryExpr   != nil => return @PrimaryExpr.Eval() + @SecondaryExpr.Eval()
		""

struct SecondaryExpr
	*Node
	Selector      string
	Index         *Index
	Slice         *Slice
	TypeAssertion *TypeAssertion
	Arguments     *Arguments
	Eval: string ->
		if len(@Selector) > 0        => return @Selector
		if @Index             != nil => return @Index.Eval()
		if @Slice             != nil => return @Slice.Eval()
		if @TypeAssertion     != nil => return @TypeAssertion.Eval()
		if @Arguments         != nil => return @Arguments.Eval()
		""

struct Index
	*Node
	Expression *Expression
	Eval: string -> "[" + @Expression.Eval() + "]"

struct Slice
	*Node
	LeftExpr   *Expression
	MiddleExpr *Expression
	RightExpr  *Expression
	Eval: string ->
		res := "["
		if @LeftExpr != nil => res += @LeftExpr.Eval()
		res += ":"
		if @MiddleExpr != nil => res += @MiddleExpr.Eval()
		if @RightExpr != nil => res += ":" + @RightExpr.Eval()
		res + "]"

struct TypeAssertion
	*Node
	Type *Type
	Eval: string -> ".(" + @Type.Eval() + ")"

struct Arguments
	*Node
	Expressions *ExpressionList
	Type        *Type
	IsVariadic  bool
	Eval: string ->
		res := "("
		if @Type != nil
			res += @Type.Eval()
			if @Expressions != nil
				res += ","
		if @Expressions != nil => res += @Expressions.Eval()
		if @IsVariadic         => res += "..."
		res + ")"

struct MethodExpr
	*Node
	ReceiverType *ReceiverType
	Name         string
	Eval: string -> @ReceiverType.Eval() + "." + @Name

struct ReceiverType
	*Node
	Type         string
	IsPointer    bool
	ReceiverType *ReceiverType
	Eval: string ->
		res := ""
		if @ReceiverType != nil => return "(" + @ReceiverType.Eval() + ")"
		res += @Type
		if @IsPointer => res = "(*" + res + ")"
		res

struct Expression
	*Node
	UnaryExpr       *UnaryExpr
	// FunctionLit     *FunctionLit
	LeftExpression  *Expression
	Op              string
	RightExpression *Expression
	Eval: string ->
		if @UnaryExpr   != nil => return @UnaryExpr.Eval()
		// if @FunctionLit != nil => return @FunctionLit.Eval()
		@LeftExpression.Eval() + @Op + @RightExpression.Eval()

struct UnaryExpr
	*Node
	PrimaryExpr *PrimaryExpr
	Op          string
	UnaryExpr   *UnaryExpr
	Eval: string ->
		if @PrimaryExpr != nil => return @PrimaryExpr.Eval()
		@Op + @UnaryExpr.Eval()

struct Conversion
	*Node
	Type       *Type
	Expression *Expression
	Eval: string -> @Type.Eval() + "(" + @Expression.Eval() + ")"

struct Interpret
	*Node
	Statement *Statement
	TopLevel  *TopLevel
	Eval: string ->
		if @Statement != nil => return @Statement.Eval()
		if @TopLevel != nil => return @TopLevel.Eval()
		""
